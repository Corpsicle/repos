'Example using stack to get recursively (all sub directories)  jpg's and store to a file

rootDir = "C:\Users\Public\Pictures"
outputFile = Program.Directory+"\piclist.txt"

'Add root directory to the Stack as the first to check
Stack.PushValue("dirs",rootDir)

'Delete the output file first (if it exists)
File.DeleteFile(outputFile)

'Recursively find all jpg in current and all sub directories
While (Stack.GetCount("dirs") > 0)
  'Current directly to be searched
  dirName = Stack.PopValue("dirs")
  
  'Find all files ending in jpg and add then to the output file
  files = File.GetFiles(dirName)
  For i = 1 To Array.GetItemCount(files)
    fileName = files[i]
    If (Text.EndsWith(Text.ConvertToLowerCase(fileName),".jpg")) Then
      File.AppendContents(outputFile, fileName)
    EndIf
  EndFor
  
  'Add all sub directories to the Stack to be handled as the stack unwinds
  subDir = File.GetDirectories(dirName)
  For i = 1 To Array.GetItemCount(subDir)
    Stack.PushValue("dirs",subDir[i])
  EndFor  
EndWhile
