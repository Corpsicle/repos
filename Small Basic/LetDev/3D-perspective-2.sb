'More 3D perspective

'Set graphics window
gw = 800'Desktop.Width
gh = 600'Desktop.Height
GraphicsWindow.Width = gw
GraphicsWindow.Height = gh
GraphicsWindow.CanResize = "false"
GraphicsWindow.BackgroundColor = "LightBlue"
GraphicsWindow.MouseMove = OnMouseMove
GraphicsWindow.PenColor = "Black"
For i=1 To 500
  x = gw/2+gw*Math.Cos(2*3.142*i/500)
  y = gh/2+gh*Math.Sin(2*3.142*i/500)
  GraphicsWindow.DrawLine(x,y,gw/2,gh/2)
EndFor

'Set balls array
nball = 100
Smin=10
Smax=80
Zmin=0.3
Zmax=10
Vmax=.1
For i = 1 To nball
  size = Smin+i/nball*(Smax-Smin)
  GraphicsWindow.BrushColor=GraphicsWindow.GetColorFromRGB(254*i/nball,0,0)
  ball = shapes.AddEllipse(size,size)
  balls[i] = ball
  X[i] = gw/8+Math.GetRandomNumber(gw/4)
  Y[i] = X[i]
  S[i] = size
  Z[i] = Zmin+(Zmax-Zmin)*(1-i/nball)
  A[i] = Math.GetRandomNumber(2*3.14)
  area= (X[i]-gw/2)/(gw/2)*(Y[i]-gh/2)/(gh/2)
  V[i] = Vmax*Math.Abs(area)
EndFor

'Move balls
t=0
While("true")
  For i=1 To nball
    x1 = gw/2+X[i]*Math.Cos(A[i]+t*V[i])
    y1 = gh/2+Y[i]*Math.Sin(A[i]+t*V[i])
    ball = balls[i]
    xS = x1-S[i]/2-Xeye/Z[i]
    yS = y1-S[i]/2-Yeye/Z[i]
    Shapes.Move(ball,xS,yS)
  EndFor
  Program.Delay(5)
  t=t+1
EndWhile

Sub OnMouseMove
  Xeye=GraphicsWindow.MouseX-gw/2
  Yeye=GraphicsWindow.MouseY-gh/2
EndSub

